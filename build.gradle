buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    // GRADLE DEPENDENCIES
    // SUPPORT
    //noinspection GradleDependency
    compile 'com.android.support:support-v4:19.0.1'
    // HTTP COMPONENTS
    compile 'org.apache.httpcomponents:httpcore:4.3.2'
    compile 'org.apache.httpcomponents:httpmime:4.3.4'

    // JAR DEPENDENCIES
    // VOLLEY
    compile files('libs/volley.jar')
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion = project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

group = 'fr.baloomba'
version = '1.0.0'

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'wsvolley'
            packaging 'aar'
            description 'Google volley interface'
            url 'https://github.com/baloomba/wsvolley'

            scm {
                url 'scm:git@github.com:baloomba/WSVolley.git'
                connection 'scm:git@github.com:baloomba/WSVolley.git'
                developerConnection 'scm:git@github.com:baloomba/WSVolley.git'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }

            developers {
                developer {
                    id 'baloo'
                    name 'Clement BOULY'
                    email 'bouly.clement@gmail.com'
                }
            }
        }
    }
}
